buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
	}
}

plugins {
	id "com.matthewprenger.cursegradle" version "1.0.6"
	id "org.ajoberstar.grgit" version "1.3.2"
}

apply plugin: "net.minecraftforge.gradle.forge"

import org.ajoberstar.grgit.*

ext.repo = Grgit.open(file("."))

version = mc_version + "-" + mod_version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings

	useDepAts = true
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", mc_version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": mc_version
	}

	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

repositories {
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts/"
	}
	maven {
		name "shadowfacts"
		url "http://maven.amadornes.com/"
	}
	maven {
		name "blay09"
		url "http://repo.blay09.net/"
	}
	maven {
		name "progwml6"
		url "http://dvs1.progwml6.com/files/maven/"
	}
}

dependencies {
	deobfCompile group: "net.shadowfacts", name: "ShadowMC", version: mc_version + "-" + shadowmc_version
	deobfCompile group: "MCMultiPart", name: "MCMultiPart-experimental", version: mcmp_version, classifier: "universal"
	deobfCompile group: "net.blay09.mods", name: "craftingtweaks", version: ct_version
	deobfCompile group: "mezz.jei", name: "jei_1.8.9", version: jei_version
}

task(release, dependsOn: ["generateChangelog", "tagRelease", "curseforge"])

task(generateChangelog) << {
	def changelog = ""
	def commits = repo.log {
		range(file("gradle/last-release-commit.txt").text, "HEAD")
	}
	commits.each {
		changelog = "- " + it.fullMessage + "\n" + changelog
	}
	file("changelog.txt").text = changelog
	file("gradle/last-release-commit.txt").text = commits.first().id
}

task(tagRelease) << {
	repo.tag.add {
		name = version
		message = "Release ${version}"
	}
	repo.push(tags: true)
}

tasks.generateChangelog.mustRunAfter tagRelease

curseforge {
	apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""

	project {
		id = "236326"

		addGameVersion "1.8.8"
		addGameVersion "1.8.9"

		mainArtifact(jar) {
			releaseType = "release"
			changelog = file("changelog.txt").text

			relations {
				requiredLibrary "shadowmc"
			}
		}
	}
}

tasks.curseforge.mustRunAfter generateChangelog