buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
	}
}

plugins {
	id "com.matthewprenger.cursegradle" version "1.0.6"
}

apply plugin: "net.minecraftforge.gradle.forge"

version = mc_version + "-" + mod_version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings

	useDepAts = true
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", mc_version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": mc_version
	}

	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

repositories {
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts/"
	}
	maven {
		name "amadornes"
		url "http://maven.amadornes.com/"
	}
	maven {
		name "progwml6"
		url "http://dvs1.progwml6.com/files/maven/"
	}
	maven {
		name "eiranet"
		url "http://repo.blay09.net"
	}
}

dependencies {
	deobfCompile group: "net.shadowfacts", name: "ShadowMC", version: mc_version + "-" + shadowmc_version
	deobfCompile group: "MCMultiPart", name: "MCMultiPart", version: mcmp_version, classifier: "universal"
	deobfCompile group: "mezz.jei", name: "jei_1.9", version: jei_version
	deobfCompile group: "net.blay09.mods", name: "craftingtweaks", version: "4.0.10"
}

curseforge {
	apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""

	project {
		id = "236326"

		addGameVersion "1.9"

		addGameVersion "Java 8"

		mainArtifact(jar) {
			releaseType = "release"
			changelog = file("changelog.txt").text

			relations {
				requiredLibrary "shadowmc"
			}
		}
	}
}