// Plugins
plugins {
	id "net.minecraftforge.gradle.forge" version "2.0.2"
	id "maven"
}

// Config
file('build.properties').withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse(prop)
}

// More plugins
apply from: "gradle/scripts/dependencies.gradle"
apply from: "gradle/scripts/artifacts.gradle"

// Info
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = config.mc_version + "-" + config.mod_version
group = config.group
archivesBaseName = config.archivesBaseName

// Minecraft/Forge
minecraft {
	version = "${config.mc_version}-${config.forge_version}"
	runDir = "run"
	mappings = "snapshot_20141130"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}